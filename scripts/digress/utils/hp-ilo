#!/usr/bin/perl
# This is a wrapper around the locfg.pl from HP. It's pretty silly to do
# this as a wrapper, but that program is not licensed and I didn't feel
# like doing a clean room reimplementation of it.
# 
# This wrapper knows how to do different simple actions, controlled by the
# first parameter. It generates the XML commands and feeds them to
# locfg.pl. See the top-level README for documentation of the environment
# variables it expects to be set.
#
# GPL Copyright 2004 by Joey Hess.

use strict;
use IPC::Open2;

my $action=shift;
if ($action eq 'reset') {
	print "Resetting..\n";
	send_command(qq{<RESET_SERVER/>});
	exit process_result();
}
elsif ($action eq 'poweron') {
	print "Powering on..\n";
	send_command(qq{<SET_HOST_POWER HOST_POWER="Yes"/>});
	exit process_result();
}
elsif ($action eq 'poweroff') {
	print "Powering off..\n";
	send_command(qq{<SET_HOST_POWER HOST_POWER="No"/>});
	exit process_result();
}
elsif ($action eq 'force_restart') {
	send_command(qq{<GET_HOST_POWER_STATUS/>});
	if (scan_result("HOST_POWER") eq 'OFF') {
		print "Powering on..\n";
		send_command(qq{<SET_HOST_POWER HOST_POWER="Yes"/>});
	}
	else {
		print "Restarting..\n";
		send_command(qq{<RESET_SERVER/>});
	}
	exit process_result();
}
elsif ($action eq 'get_host_power') {
	send_command(qq{<GET_HOST_POWER_STATUS/>});
	print "Power: ".scan_result("HOST_POWER")."\n";
}
else {
	print STDERR "unnown action $action\n";
}

sub send_command {
	my $command=gen_command(@_);

	if (! defined $ENV{HP_ILO_HOST}) {
		die "Please set HP_ILO_HOST\n";
	}

	my $pid = open2 (\*RDRFH, \*WTRFH, "locfg.pl", "-s", $ENV{HP_ILO_HOST}, "-f", "-");
	if (! $pid) {
		die "failed to run locfg.pl: $!\n";
	}

	print WTRFH $command;
	close WTRFH;
}

sub scan_result {
	my $token=shift;
	my $ret;
	while (<RDRFH>) {
		if (/$token="(.*?)"/) {
			$ret=$1;
		}
	}
	return $ret;
}

sub process_result {
	my $error;
	while (<RDRFH>) {
		if (/MESSAGE='(.*?)'/) {
			my $msg=$1;
			if ($msg=~/error/i && lc($msg) ne "no error") {
				$error=1;
			}
			elsif (lc($msg) eq 'no error') {
				$error=0 unless defined $error;
			}
			else {
				print "Message: $msg\n";
			}
		}
	}
	close RDRFH;

	if (! defined $error) {
		print STDERR "Parse failure or locfg run failed.\n";
	}
	
	if (! defined $error || $error) {
		return 1;
	}
	else {
		return 0;
	}
}

sub gen_command {
	my $password=getpassword();
	my $user=getuser();
	return <<"EOF"
<RIBCL VERSION="2.0">
	<LOGIN USER_LOGIN="$user" PASSWORD="$password">
		<SERVER_INFO MODE="write">
			@_
		</SERVER_INFO>
	</LOGIN>
</RIBCL>
EOF
}

sub getpassword {
	if (exists $ENV{HP_ILO_PASSWORD}) {
		return $ENV{HP_ILO_PASSWORD};
	}
	else {
		open (PW, $ENV{HP_ILO_PASSWORD_FILE}) || die "Please set HP_ILO_PASSWORD or HP_ILO_PASSWORD_FILE\n";
		my $password=<PW>;
		chomp $password;
		close PW;
		return $password;
	}
}

sub getuser {
	if (exists $ENV{HP_ILO_USER}) {
		return $ENV{HP_ILO_USER}
	}
	die "Please set HP_ILO_USER\n";
}
