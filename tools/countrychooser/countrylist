#!/usr/bin/perl
use strict;
use warnings;

# Builds a list of countries, sorted into regions.
my $iso3166tab=shift;
my $regionmap=shift;
my $indent=shift || "   ";

my %code2country;
my %codes_listed;
open (L, $iso3166tab) || die "$iso3166tab: $!";
while (<L>) {
	chomp;
	my ($code, $country) = split(' ', $_, 2);
	$code2country{$code}=$country;
	$codes_listed{$code}=0;
}

my %regions;
open (R, $regionmap) || die "$regionmap: $!";
while (<R>) {
	chomp;
	my ($code, $region) = split(' ', $_, 2);
	if (exists $code2country{$code}) {
		push @{$regions{$region}}, $code2country{$code};
		$codes_listed{$code}++;
	}
	elsif ($code ne 'unlisted') {
		print STDERR "skipping unknown country code, $code, in $regionmap but not in $iso3166tab\n";
	}
}
close R;

# Add any unlisted countries to "other".
foreach my $code (keys %codes_listed) {
	if ($codes_listed{$code} == 0) {
		push @{$regions{unknown}}, $code2country{$code};
		print STDERR "unknown region for country $code, not in $regionmap\n";
	}
}

foreach my $region (sort keys %regions) {
	print "$region\n";
	foreach my $country (sort @{$regions{$region}}) {
		print "$indent$country\n";
	}
}
