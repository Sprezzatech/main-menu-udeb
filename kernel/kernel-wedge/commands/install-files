#!/usr/bin/perl -w
# Create and populate the package build directories.

sub doit {
	print "\t".join(" ", @_)."\n";
	my $ret=(system(@_) >> 8);
	if ($ret != 0) {
		die "command exited with status $ret\n";
	}
}

my $hostarch=`dpkg-architecture -qDEB_HOST_ARCH`;
chomp $hostarch;

my $sourcedir=$ENV{SOURCEDIR};
if (! defined $sourcedir) {
	$sourcedir="";
}

my $kname;

open(NAMES, "/usr/share/kernel-wedge/kernel-names") || die "kernel-names: $!";
while (<NAMES>) {
	chomp;
	next if /^#/ || ! length;

	my ($arch, $name)=split(' ', $_, 2);
	if ($arch eq $hostarch) {
		$kname=$name;
		last;
	}
}
close NAMES;

if (! defined $kname) {
	die "could not find $hostarch in kernel-names";
}

open(KVERS, "kernel-versions") || die "kernel-versions: $!";
while (<KVERS>) {
	chomp;
	next if /^#/ || ! length;

	my ($arch, $kernelversion, $flavour, $installedname, $multkern, $builddep)=split(' ', $_, 6);
	if (! length $arch || ! length $kernelversion ||
	    ! length $installedname || ! length $flavour || 
	    ! length $builddep ) {
		die "parse error: $_";
	}
	next unless $arch eq $hostarch;

	my $extraname="";
	if ($multkern =~ /^[yY]/) {
		$extraname="-$kernelversion-$flavour";
	}
	
	# Temporary gross hack for netwinder, has only a vmlinux.
	if ($installedname =~ /netwinder/) {
		doit("install", "-D", "-m", 644,
			"$sourcedir/boot/vmlinux-$installedname",
			"debian/kernel-image-$kernelversion-$flavour-di/boot/$kname$extraname");
	}
	else {
		doit("install", "-D", "-m", 644,
			"$sourcedir/boot/$kname-$installedname",
			"debian/kernel-image-$kernelversion-$flavour-di/boot/$kname$extraname");
	}
	doit("install", "-D", "-m", 644,
		"$sourcedir/boot/System.map-$installedname",
		"debian/kernel-image-$kernelversion-$flavour-di/boot/System.map$extraname")
	      if -e "$sourcedir/boot/System.map-$installedname";
	doit("kernel-wedge", "copy-modules", $kernelversion, $flavour, $installedname);
	doit("kernel-wedge", "find-dups", "$kernelversion-$flavour");
}
close KVERS;
