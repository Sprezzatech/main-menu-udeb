#!/usr/bin/perl
# Outputs a list of udebs to install. Pass it the type of image to build as
# the first parameter. The second parameter can point to a different image
# type which the image is a driver disk for. Next the kernel flavour, then
# the major kernel type (2.4, 2.6, etc), then the sub architecture (or an
# empty string if there are none) and then the kernel version(s) as the rest
# of the parameters. Reads the lists in pkg-lists.
use warnings;
use strict;

if (int(@ARGV) < 5) {
	die "Usage: $0 type DRIVER_FOR KERNEL_FLAVOUR KERNELMAJOR SUB_ARCH KERNEL_VERSION [KERNEL_VERSION ...]\n";
}
my $type=shift;
my $driver_for=shift;
my $kernel_flavour=shift;
my $kernel_major=shift;
my $sub_arch=shift;
my @kernel_versions=@ARGV;

my $deb_host_arch=`dpkg-architecture -qDEB_HOST_ARCH`;
chomp $deb_host_arch;

my ($collect, $exclude)=getlists($type);
my ($parentcollect, $parentexclude)=getlists($driver_for) if length $driver_for;
foreach my $p (sort keys %$collect) {
	print "$p\n" unless $exclude->{$p} || $parentcollect->{$p};

	# Warn about missing deps.
	foreach my $deplist (pkgdeps($p)) {
		my $ok=0;
		foreach my $dep (@$deplist) {
			if ($collect->{$dep} || $exclude->{$dep} || $parentcollect->{$dep}) {
				$ok=1;
			}
		}
		if (! $ok) {
			warning("in $type, $p has unsatisfied dependency on ".
				join(" | ", @$deplist));
		}
	}
}

# Run apt-cache on udebs, return result.
sub apt_cache {
	my $params=shift;
	
	my $sourceslist;
	if (-f 'sources.list.udeb.local') {
		$sourceslist='sources.list.udeb.local';
	} else {
		$sourceslist='sources.list.udeb';
	}
	
	return `LANG=C apt-cache \\
		-o Dir::Etc::sourcelist=./$sourceslist \\
		-o Dir::State=./apt.udeb/state \\
		-o Dir::State::Status=./apt.udeb/state/status \\
		-o Dir::Cache=./apt.udeb/cache $params 2>/dev/null`;
}

# Add a package, possibly expanding dependencies.
sub collectpackage {
	my $line=shift;
	my $collect=shift;
	my $exclude=shift;
	my $postponed=shift;

	if ($line=~s/^(.*) \[([0-9. ]+)\]$/$1/) {
		my %kernels=();
		$kernels{$_} = 1 foreach split ' ', $2;
		return unless $kernels{$kernel_major};
	}
	elsif ($line=~s/\s*\?$//) {
		# Question mark at the end means check for the package and
		# don't include it if it's not available in apt sources.
		return unless length apt_cache("show '$line'");
	}

	if ($line=~/^(.*) \*$/) {
		# Asterisk at end means include all dependencies of this
		# package.
		$collect->{$1}=1;
		collectdeps($1, $collect, $postponed);
	}
	elsif ($line=~/^(.*) \-$/) {
		# Minus sign at the end means exclude this package from the
		# list.
		$exclude->{$1}=1;
	}
	else {
		$collect->{$line}=1;
	}
}

# Recursively add dependencies of package.
sub collectdeps {
	my $package=shift;
	my $collect=shift;
	my $postponed=shift;
	my %seendeps;
	%seendeps=%{shift()} if @_;
	return if $seendeps{$package};
	$seendeps{$package}=1;
	foreach my $deplist (pkgdeps($package)) {
		if (@$deplist > 1) {
			# If there's more than one alternative to satisfy a
			# dep, postpone trying to satisfy it until later.
			push @{$postponed}, $deplist;		
		}
		else {
			$collect->{$deplist->[0]}=1;
			collectdeps($deplist->[0], $collect, $postponed, \%seendeps);
		}
	}
}

# Get the dependencies of a package. Returns a list of sublists; at least
# one item from each sublist is needed to satisfy the dependencies.
sub pkgdeps {
	my $package=shift;
	my @ret;
	my @lines=map { chomp $_; $_ } apt_cache("depends $package");
	shift @lines; # package name;
	for (my $x=0; $x < @lines; $x++) {
		my $dep=$lines[$x];
		# Note that this intentionally skips alternate
		# dependencies, since udebs are not allowed to have
		# alternates.
		if ($dep=~/^\s*Depends:\s/) {
			$dep=~s/^\s*Depends:\s*//;
			if ($dep=~/\<.+\>/) {
				# Virtual package, so collect all providers.
				my @deps;
				for ($x++; $x < @lines; $x++) {
					if ($lines[$x]=~/^    (.+)/) {
						push @deps, $1;
					}
					else {
						$x--;
						last;
					}
				}
				if (@deps) {
					push @ret, \@deps;
				}
			}
			else {
				push @ret, [$dep];
			}
		}
	}
	return @ret;
}

# Return two hash references, one of udebs to include, one to exclude.
sub getlists {
	my $type=shift;

	my %collect;
	my %exclude;
	my @postponed;
	
	my @lists = ("pkg-lists/local", "pkg-lists/exclude");
	my $t="";
	foreach my $subtype (split "/", $type) {
		if (! length $t) {
			$t=$subtype;
		}
		else {
			$t="$t/$subtype";
		}
		push @lists, ("pkg-lists/$t/local", "pkg-lists/$t/common",
		              "pkg-lists/$t/$deb_host_arch.cfg");
		push @lists, "pkg-lists/$t/$deb_host_arch/$sub_arch.cfg" if $sub_arch;
	}

	while (@lists) {
		my $list=pop @lists;
		if (! -e $list) {
			warning("missing list, $list, for type $type")
				if $list !~ /local$/ && $list !~ m#$deb_host_arch/$sub_arch.cfg$#;
		}
		else {
			open (LIST, $list) || die "open $list $!";
			while (<LIST>) {
				chomp;
	
				# includes
				if (/^#include \"(.*)\"/) {
					my $include=$1;
					if (-e "pkg-lists/kernel_specific/$kernel_major/$deb_host_arch/$sub_arch/$include") {
						push @lists, "pkg-lists/kernel_specific/$kernel_major/$deb_host_arch/$sub_arch/$include";
					}
					elsif (-e "pkg-lists/kernel_specific/$kernel_major/$deb_host_arch/$include") {
						push @lists, "pkg-lists/kernel_specific/$kernel_major/$deb_host_arch/$include";
					}
					elsif (-e "pkg-lists/kernel_specific/$kernel_major/$include") {
						push @lists, "pkg-lists/kernel_specific/$kernel_major/$include";
					}
					else {
						push @lists, "pkg-lists/$include";
					}
				}
				
				# comments
				s/^#.*//;
				next unless length;
				
				# normal kernel version substitution
				if (/\${kernel:Version}/) {
					foreach my $v (@kernel_versions) {
						my $l=$_;
						$l=~s/\${kernel:Version}/$v-$kernel_flavour/g;
						collectpackage($l, \%collect, \%exclude, \@postponed);
					}
					next; # move on to the next line
				}
				collectpackage($_, \%collect, \%exclude, \@postponed);
			}
			close LIST;
		}
	}

	# Make sure any postponed sets of alternative dependencies
	# are satisfied. If any of the dependencies in a set are already
	# selected, that satisfies it. If now, arbitrarily choose the
	# first.
	foreach my $set (@postponed) {
		my $satisfied=0;
		foreach my $d (@$set) {
			if ($collect->{$d}) {
				$satisfied=1;
			}
		}
		if (! $satisfied) {
			$collect->{$set->[0]}=1;
		}
	}
	
	return \%collect, \%exclude;
}

sub warning {
	print STDERR "** warning: @_\n";
}
