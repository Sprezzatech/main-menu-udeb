#!/usr/bin/perl
# Outputs a list of udebs to install. Pass it the type of image to build as
# the first parameter, and the kernel version(s) as the rest of the
# parameters. Reads the lists in pkg-lists.
my $type=shift;
my @kernel_versions=@ARGV;

my $deb_host_arch=`dpkg-architecture -qDEB_HOST_ARCH`;
chomp $deb_host_arch;

my @lists = ("pkg-lists/$type/common", "pkg-lists/$type/$deb_host_arch");
while (@lists) {
	my $list=pop @lists;
	if (! -e $list) {
		die "missing list, $list, for type $type";
	}
	open (LIST, $list) || die "open $list $!";
	while (<LIST>) {
		chomp;

		# includes
		if (/^#include \"(.*)\"/) {
			push @lists, "pkg-lists/$1";
		}
		
		# comments
		s/^#.*//;
		next unless length;
		
		# kernel version substitution
		if (/\${kernel:Version}/) {
			foreach my $v (@kernel_versions) {
				my $l=$_;
				$l=~s/\${kernel:Version}/$v/g;
				print "$l\n";
			}
		}
		else {
			print "$_\n";
		}
	}
	close LIST;
}
